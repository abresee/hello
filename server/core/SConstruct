# vim: set syntax=python
import os

boostincl=' -I'+os.environ.get('BOOSTINCL','')
boostlibs=' -L'+os.environ.get('BOOSTLIBS','')

core_sources = ['Player.cpp', 'Instrument.cpp', 'WaveGenerator.cpp', 'Note.cpp','Config.cpp']

env=Environment()
env['CXX']=os.environ.get('CXX','g++')

env.ParseConfig('pkg-config gstreamer-1.0 gstreamer-plugins-base-1.0 --cflags --libs')
env.MergeFlags({ 
     'CXXFLAGS': ['-std=c++11']
    ,'CCFLAGS' : ['-g']
})
env.Append(LIBS='gstapp-1.0')
env_py=env.Clone()
env_py.ParseConfig('python3.3-config --cflags --ldflags')
env_py.MergeFlags(env.ParseFlags('-fPIC '+boostincl+' '+boostlibs+' -lboost_python3'))
env_py['CCFLAGS'].remove('-Wstrict-prototypes')
env_py['CCFLAGS'].remove('-O2')
env_py['CCFLAGS'].remove('-g')
env_py['CPPDEFINES'].remove('NDEBUG')
prelude=env.Program('prelude',core_sources+['demos/prelude.cpp'])
chromatic=env.Program('chromatic',core_sources+['demos/chromatic.cpp'])
note_test=env.Program('note_test',['Note.cpp','tests/Note_test.cpp'])

hello_gstreamer_so=env_py.SharedLibrary('core_py',core_sources+['core_py.cpp'],SHLIBPREFIX='',SHLIBSUFFIX='.so')
